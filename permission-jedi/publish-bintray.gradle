/**
 * Publish to JCentral+Maven Central via Bintray
 *
 * Prework for Bintray
 * 1. Register to Bintray and set up auto-signing:
 *      a) Generate yourself a keypair, if you don’t have one.
 *      https://help.github.com/articles/generating-a-new-gpg-key/
 *      https://central.sonatype.org/pages/working-with-pgp-signatures.html
 *      b) Add GPG keypair to your profile at tab GPGUserSigning
 *      https://bintray.com/profile/edit
 * 2) Add your Sonatype account under “accounts”.
 *      a) Create your Sonatype account under “accounts”
 *      b) Setup your default Maven repo with your GPG key signing
 *      https://central.sonatype.org/pages/ossrh-guide.html#initial-setup
 * 3. Create and link your package:
 *      a) Import from a GitHub repo(or create a new Maven package)
 *      b) Click on “Add to JCenter”
 * 4. Set up Maven to deploy to Bintray by using pom.xml snippets from “Set me up!” guide, or use the bintray-gradle-plugin.
 *
 * Deploying via Bintray with Gradle refer:
 * https://medium.com/@yegor_zatsepin/simple-way-to-publish-your-android-library-to-jcenter-d1e145bacf1
 */

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'signing'

def POM_NAME = 'permission-jedi'
def POM_ARTIFACT_ID = 'permission-jedi'
def POM_GROUP_ID = 'com.github.kopihao'
def POM_VERSION = project._versionName
def POM_DESCRIPTION = POM_GROUP_ID + ':' + POM_ARTIFACT_ID + ':' + POM_VERSION
def POM_URL = 'https://github.com/kopihao/permission-jedi'
def POM_GIT_URL = 'https://github.com/kopihao/permission-jedi.git'
def POM_CONNECTION = 'scm:git@github.com:kopihao/permission-jedi.git'
def POM_LICENSE_ALL = ["Apache-2.0"]
def POM_LICENSE_NAME = 'The Apache Software License, Version 2.0'
def POM_LICENSE_URL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
def POM_DEVELOPER_ID = 'kopihao.my'
def POM_DEVELOPER_NAME = 'Kopihao'
def POM_DEVELOPER_EMAIL = 'kopihao@gmail.com'
def POM_PACKAGING = 'aar'

group = POM_GROUP_ID
version = POM_VERSION
println 'Uploading ' + POM_DESCRIPTION + " via Bintray"

install {
    repositories.mavenInstaller {
        pom.project {
            name POM_NAME
            packaging POM_PACKAGING
            groupId POM_GROUP_ID
            artifactId POM_ARTIFACT_ID
            description POM_DESCRIPTION
            url POM_URL
            scm {
                url POM_URL
                connection POM_CONNECTION
                developerConnection POM_CONNECTION
            }
            licenses {
                license {
                    name POM_LICENSE_NAME
                    url POM_LICENSE_URL
                }
            }
            developers {
                developer {
                    id POM_DEVELOPER_ID
                    name POM_DEVELOPER_NAME
                    email POM_DEVELOPER_EMAIL
                }
            }
        }
    }
}

// Uncomment to use local.properties
//def propertiesFileName = 'local.properties'
//Properties properties = new Properties();
//properties.load(project.rootProject.file(propertiesFileName).newDataInputStream());

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
signing {
    sign configurations.archives
}

//signing {
//    sign configurations.archives
//}

/**
 * kopihao:
 * Beware when creating your android library module,
 * your artifact name should identical as module name
 * Otherwise, your library files and pom will be uploaded to different hierarchy.
 * I realized only when I learn to check from the following:
 * http://jcenter.bintray.com/com/jakewharton/butterknife
 * http://jcenter.bintray.com/com/loopj/android/android-async-http
 */

def BINTRAY_REPO = 'maven'
def BINTRAY_USER = bintrayUser
def BINTRAY_API_KEY = bintrayAPIKey
def BINTRAY_NAME = POM_ARTIFACT_ID
def BINTRAY_DESCRIPTION = POM_DESCRIPTION
def BINTRAY_URL = POM_URL
def BINTRAY_GIT_URL = POM_GIT_URL
def BINTRAY_LICENSE_ALL = POM_LICENSE_ALL

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY
    configurations = ['archives']
    pkg {
        repo = BINTRAY_REPO
        name = BINTRAY_NAME
        desc = BINTRAY_DESCRIPTION
        websiteUrl = BINTRAY_URL
        vcsUrl = BINTRAY_GIT_URL
        licenses = BINTRAY_LICENSE_ALL
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = BINTRAY_DESCRIPTION
        }
    }
}